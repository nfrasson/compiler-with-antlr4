grammar FrassonGrammar;

@header {
	import java.util.ArrayList;
	import java.util.Stack;
	import java.util.HashMap;
	import io.compiler.types.*;
	import io.compiler.core.ast.*;
}

@members {
    private HashMap<String,Var> symbolTable = new HashMap<String, Var>();
    private ArrayList<Var> currentDecl = new ArrayList<Var>();
    private Types currentType;
    private Types leftType=null, rightType=null;
    private Program program = new Program();
    private String strExpr = "";
    private CommandIf currentCommandIf;
    
    private Stack<ArrayList<Command>> stack = new Stack<ArrayList<Command>>();
    
    
    public void updateType(){
    	for(Var v: currentDecl){
    	   v.setType(currentType);
    	   symbolTable.put(v.getId(), v);
    	}
    }
    
    public Program getProgram(){
    	return this.program;
    	}
    
    public boolean isDeclared(String id){
    	return symbolTable.get(id) != null;
    }
}
 
programa	: 'programa' ID  { program.setName(_input.LT(-1).getText());
                               stack.push(new ArrayList<Command>()); 
                             }
               declaravar+
               'inicio'
               comando+
               'fim'
               'fimprog'
               
               {
                  program.setSymbolTable(symbolTable);
                  program.setCommandList(stack.pop());
               }
			;
						
declaravar	: 'declare' { currentDecl.clear(); } 
               ID  { currentDecl.add(new Var(_input.LT(-1).getText()));}
               ( VIRG ID                
              		 { currentDecl.add(new Var(_input.LT(-1).getText()));}
               )*	 
               DP 
               (
               'number' {currentType = Types.NUMBER;}
               |
               'text' {currentType = Types.TEXT;}
               |
               'real' {currentType = Types.REALNUMBER;}
               ) 
               
               { updateType(); } 
               PV
			;
			
comando     :  cmdAttrib
			|  cmdLeitura
			|  cmdEscrita
			|  cmdIF
            |  cmdWhile
			;
			
cmdWhile
		    : 'enquanto' 
		      { 
		      	  CommandWhile cmdWhile = new CommandWhile();
		          stack.push(new ArrayList<Command>());
		          strExpr = "";
		      } 
		      AP 
		      expr 
		      OPREL  
		      { 
		          strExpr += _input.LT(-1).getText();
		      } 
		      expr 
		      FP 
		      { 
		          System.out.println("Express√£o do while: " + strExpr);
		          cmdWhile.setExpression(strExpr);
		      }
		      'faca'  
		      comando+ 
		      {
		          cmdWhile.setLoopCommands(stack.pop());
		          stack.peek().add(cmdWhile);
		      }  
		      'fimenquanto'
		    ;
			
cmdIF		: 'se'  { stack.push(new ArrayList<Command>());
                      strExpr = "";
                      currentCommandIf = new CommandIf();
                    } 
               AP 
               expr
               OPREL  { strExpr += _input.LT(-1).getText(); }
               expr 
               FP  { currentCommandIf.setExpression(strExpr); }
               'entao'  
               comando+                
               { 
                  currentCommandIf.setTrueList(stack.pop());                            
               }  
               ( 'senao'  
                  { stack.push(new ArrayList<Command>()); }
                 comando+
                 {
                   currentCommandIf.setFalseList(stack.pop());
                 }  
               )? 
               'fimse' 
               {
               	   stack.peek().add(currentCommandIf);
               }  			   

			;
			
cmdAttrib
		    : ID 
		      { 
		      	  strExpr = "";
		          if (!isDeclared(_input.LT(-1).getText())) {
		              throw new RuntimeException("Undeclared Variable: " + _input.LT(-1).getText());
		          }
		          
		          symbolTable.get(_input.LT(-1).getText()).setInitialized(true);

		          leftType = symbolTable.get(_input.LT(-1).getText()).getType();
		
		          String varName = _input.LT(-1).getText();
		      }
		    OP_AT 
		    expr 
		    PV 
		    {
		        if (leftType.getValue() < rightType.getValue()) {
		            throw new RuntimeException("Type Mismatching on Assignment");
		        }

		        CommandAssign cmdAssign = new CommandAssign(varName, strExpr, leftType, leftType);
		        stack.peek().add(cmdAssign);
		    }
		    ;
	
			
cmdLeitura
		    : 'leia' AP 
		      ID { 
		          if (!isDeclared(_input.LT(-1).getText())) {
		              throw new RuntimeException("Undeclared Variable: "+_input.LT(-1).getText());
		          }
		          symbolTable.get(_input.LT(-1).getText()).setInitialized(true);
		          symbolTable.get(_input.LT(-1).getText()).setUsed(true);
		          
		          Command cmdRead = new CommandRead(symbolTable.get(_input.LT(-1).getText()));
		          stack.peek().add(cmdRead);
		      } 
		    FP 
		    PV
		    ;
			
cmdEscrita  : 'escreva' AP 
              ( termo  { Command cmdWrite = new CommandWrite(_input.LT(-1).getText());
                         stack.peek().add(cmdWrite);
                       } 
              ) 
              FP PV { rightType = null;}
			;			

			
expr		:  termo  { strExpr += _input.LT(-1).getText(); } exprl 			
			;
			
termo		: ID  { if (!isDeclared(_input.LT(-1).getText())) {
                       throw new RuntimeException("Undeclared Variable: "+_input.LT(-1).getText());
                    }
                    if (!symbolTable.get(_input.LT(-1).getText()).isInitialized()){
                       throw new RuntimeException("Variable "+_input.LT(-1).getText()+" has no value assigned");
                    }
                    if (rightType == null){
                       rightType = symbolTable.get(_input.LT(-1).getText()).getType();
                    }   
                    else{
                       if (symbolTable.get(_input.LT(-1).getText()).getType().getValue() > rightType.getValue()){
                          rightType = symbolTable.get(_input.LT(-1).getText()).getType();
                       }
                    }
                  }   
			| NUM    {  if (rightType == null) {
			 				rightType = Types.NUMBER;
			            }
			            else{
			                if (rightType.getValue() < Types.NUMBER.getValue()){			                    			                   				                
			                	rightType = Types.NUMBER;
			                }
			            }
			         }
			| TEXTO  {  if (rightType == null) {
			 				rightType = Types.TEXT;
			            }
			            else{
			                if (rightType.getValue() < Types.TEXT.getValue()){			                    
			                	rightType = Types.TEXT;
			                }
			            }
			         }
			;
			
exprl		: ( OP { strExpr += _input.LT(-1).getText(); } 
                termo { strExpr += _input.LT(-1).getText(); } 
              ) *
			;	
			
OP			: '+' | '-' | '*' | '/' 
			;	
			
OP_AT	    : ':='
		    ;
		    
OPREL       : '>' | '<' | '>=' | '<= ' | '<>' | '==' 
			;		    			
			
ENQUANTO    : 'enquanto'
			;

FACA        : 'faca'
			;

FIMENQUANTO : 'fimenquanto'
			;

ID			: [a-z] ( [a-z] | [A-Z] | [0-9] )*		
			;
			
NUM			: [0-9]+ ('.' [0-9]+ )?
			;			
			
VIRG		: ','
			;
						
PV			: ';'
            ;			
            
AP			: '('
			;            
						
FP			: ')'
			;
									
DP			: ':'
		    ;
		    
TEXTO       : '"' ( [a-z] | [A-Z] | [0-9] | ',' | '.' | ' ' | '-' )* '"'
			;		    

WS			: (' ' | '\n' | '\r' | '\t' ) -> skip
			;
